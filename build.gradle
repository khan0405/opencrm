group 'net.devkhan'
version '1.0-SNAPSHOT'

buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.6"
	}
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'war'
apply plugin: "com.ewerk.gradle.plugins.querydsl"

configurations {
	provided
	compile.exclude group: "commons-logging"
	compile.exclude group: "com.zaxxer", module: "HikariCP-java6"
	compile.exclude group: "commons-dbcp"
	compile.exclude group: "commons-pool"
}

idea {
	module {
		scopes.PROVIDED.plus += [configurations.provided]
	}
}

ext {
	ver = [
			java          : JavaVersion.VERSION_1_8,
			spring        : "4.2.5.RELEASE",
			springSecurity: "4.0.4.RELEASE",
			springJpa     : "1.10.1.RELEASE",
			springRedis   : "1.7.1.RELEASE",
			hibernate     : "5.1.0.Final",
			queryDsl      : "4.1.0",
			jedis         : "2.8.1",
			cglib         : "3.2.2"
	]
	queryDsl = [
			library  : "com.querydsl:querydsl-apt:$ver.queryDsl",
			hibernate: true
	]
}

querydsl {
	hibernate = queryDsl.hibernate
	library = "$queryDsl.library"
}

repositories {
	maven { url "http://nexus.goodsystems.co.kr/content/groups/public/" }
	maven { url "http://nexus.goodsystems.co.kr/content/groups/private/" }
	jcenter()
}

def profiles = ['release', 'dev', 'local']
def target = 'local'

if (project.hasProperty('target') && profiles.contains(project.target)) {
	target = project.target;
}

sourceSets {
	main {
		compileClasspath += configurations.provided
		compileClasspath += configurations.providedCompile
		resources.srcDirs = ['src/main/resources', "src/main/resources-${target}"]
	}
	test {
		compileClasspath += configurations.provided
		compileClasspath += configurations.providedCompile
		resources.srcDirs = ['src/test/resources', "src/test/resources-${target}"]
	}
}

// source code encoding
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8



dependencies {
	compile "org.projectlombok:lombok:1.16.6"
	compile 'com.google.guava:guava:19.0'

	// spring framework
	compile "org.springframework:spring-context:$ver.spring"
	compile "org.springframework:spring-webmvc:$ver.spring"
	compile "org.springframework:spring-aop:$ver.spring"
	compile "org.springframework:spring-aspects:$ver.spring"

	compile "org.springframework.data:spring-data-jpa:$ver.springJpa"
	compile "org.springframework.data:spring-data-redis:$ver.springRedis"

	compile "org.springframework.security.oauth:spring-security-oauth2:2.0.9.RELEASE"
	compile "org.springframework.security:spring-security-config:$ver.springSecurity"
	compile "org.springframework.security:spring-security-web:$ver.springSecurity"
	compile "org.springframework.security:spring-security-acl:$ver.springSecurity"
	compile "org.springframework.security:spring-security-jwt:1.0.4.RELEASE"

	compile "org.aspectj:aspectjweaver:1.8.7"
	compile "org.aspectj:aspectjrt:1.8.7"
	compile "cglib:cglib-nodep:$ver.cglib"

	// logging
	compile "ch.qos.logback:logback-classic:1.1.7"
	compile "org.slf4j:jcl-over-slf4j:1.7.20"

	// servlet api
	compile "javax.servlet:javax.servlet-api:3.1.0"

	// model mapper
	compile 'org.modelmapper:modelmapper:0.7.5'

	// hibernate orm
	compile "org.hibernate:hibernate-core:$ver.hibernate"
	compile "org.hibernate:hibernate-entitymanager:$ver.hibernate"

	// jdbc
	compile "org.mariadb.jdbc:mariadb-java-client:1.1.9"

	// dbcp
	compile("org.hibernate:hibernate-hikaricp:$ver.hibernate") {
		exclude group: "com.zaxxer"
	}
	compile "com.zaxxer:HikariCP:2.4.5"

	//querydsl jpa
	compile "com.querydsl:querydsl-jpa:$ver.queryDsl"

	// redis
	compile "redis.clients:jedis:$ver.jedis"

	// api document
	compile "io.springfox:springfox-swagger2:2.4.0"
	compile "io.springfox:springfox-swagger-ui:2.4.0"

	// json
	compile "com.fasterxml.jackson.core:jackson-databind:2.7.2"
	compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.4.0"
	compile "javax.annotation:jsr250-api:1.0"

	// testing
	testCompile("junit:junit:4.12") {
		exclude group: "org.hamcrest"
	}
	testCompile "org.springframework:spring-test:$ver.spring"
	testCompile "org.hamcrest:hamcrest-all:1.3"
}